# MiniTidal Audio Workshop 1-3 - Mini-notation Sequencing and Patterning Concepts (~1h)

Ref: 
 - https://estuary.mcmaster.ca
 - http://tidalcycles.org/docs/
 - https://tidalcycles.org/docs/patternlib/howtos/playchords/

## Setup Cells!

`!presetview twocolumns`

Choose MiniTidal from the language dropdown for both cells

## Introduction to "note" notation for adjusting pitch on your samples

So far we have focussed on rhythmic pattern control for the entirety of this course, however we can also manipulate the pitch of our samples via a number of methods - including the use of `note` notation

`note` can be used in the same way as `n` notation, but rather than affecting which sample is used from the sample folder, `note` affects the pitch of the sample

Let's introduce a new set of samples, `ukulele` by Thomas Grund - these are a set of ukulele notes tuned to concert C (ie *all the same note*) but played with different techniques, note length, vibrato etc. To prove this, let's start with our previous `n` pattern and see how it sounds

```
n "0 3 [4 5] [8 7]" # s "ukulele"
```

Some different octaves, but all tuned to C - Let's change to `note`

```
note "0 3 [4 5] [8 7]" # s "ukulele"
```

*It should be `note`d that the sample should be tuned to C to give predictable results with `note`*

The numbers used correspond to the 12 tones of the [Chromatic Scale](https://en.wikipedia.org/wiki/Chromatic_scale) used in Western Music 

However, `note` can also work with different [scales](https://tidalcycles.org/docs/patternlib/tour/harmony_melody/#scalelist)

```
note (scale "major" "<[0 1 2 3] [4 5 6 7]>") # s "ukulele"
```

In this case, each number corresponds to a degree of the **major** scale (counting from 0)

Using our knowledge of square brackets, we can create **chords**

```
note (scale "major" "[[0 1 2 4], [2 3 4 6], [4 5 6 8]]") # s "ukulele"
```

This is pretty long and unwieldy though!

Luckily you can use [chords](https://tidalcycles.org/docs/patternlib/howtos/playchords/) with `note` for much more readable (and writable) code

```
note "[c'maj d'min e'min g'maj]" # s "ukulele"
```

You can pattern chords

```
note "<[c'maj ~ d'min] e'min [~ g'maj]>" # s "ukulele"
```

Chords have some available **modifiers** too

Inversions `'i, 'ii, 'iii and so on` where the lowest note is moved to the top:

```
note "<c'maj'i c'maj'ii c'maj'iii c'maj>" # s "ukulele"
```

You can change the number of notes in the chord (it repeats notes at higher octaves)

```
note "<c'maj'6 c'maj>" # s "ukulele"
```

As the notes get higher the samples get shortened, so it's often prudent to use these modifiers in combination with a change of octave. 

By default, `c` refers to `c5` - we can change that to any relative octave eg `c4` is an octave lower

```
note "<c4'maj'6 c'maj>" # s "ukulele" -- much better!
```

Try adding some of these modifiers to our original chord progression

```
note "<[c4'maj'8 ~ d4'min'ii] e4'min'6 [~ g'maj]>" # s "ukulele"
```


---

# Exercises:

1. XXX todo 

## For Bonus Points

2. XXX todo


---
