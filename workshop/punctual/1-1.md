# Punctual Visual Workshop 1-1 - First Creations (~1h)

Ref: 
 - https://estuary.mcmaster.ca
 - https://github.com/dktr0/Punctual/blob/main/REFERENCE.md

## Setup Cells!

`!presetview fulltexteditor`

Choose Punctual from the language dropdown

## Shapes and Syntax

```
circle [0,0] 0.25 >> video;
```

Move it left and right (x axis), up and down (y axis)
```
circle [0.5,-0.8] 0.25 >> video;
```

Change the size (radius)
```
circle [0.5,-0.8] 0.75 >> video;
```

`circle` is defined in the [Punctual Reference](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md) as:
```
circle [x,y,...] [r] -- returns 1 when current fragment within a circle at x and y with radius r
```

Let's map this definition onto our example, so we can try and make sense of the definitions in the [Punctual Reference](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md) 

Firstly, we can see that `circle` matches the **function** name
```
circle ... >> video;
```

Our first set of `[ ]` brackets contain the `[x,y,...]` coordinates - this is known as an **argument** or **parameter** to a function 

```
circle [0.5,-0.8] ... >> video;
```

And the last **argument** or **parameter** is the radius `[r]` of the circle

```
circle [0.5,-0.8] 0.25 >> video;
```

## More Shapes

`vline` is a vertical line - lines in Punctual extend the width or height (in this case) of the screen, so for a `vline` we only need an `x` coordinate, and a line thickness, or `w`idth

**Note that prefixing a line with `--` creates a "comment" that Punctual will just ignore**

```
-- vline [x] [w]
vline 0 0.002 >> video;
```

Similarly for `hline` (horizontal line), except on the `y` axis
```
-- hline [y] [w]
hline 0.3 0.002 >> video;
```

`rect` is a rectangle that requires both `[x,y]` coordinates, as well as `w`idth and `h`eight arguments
```
-- rect [x,y,...] [w,h,...]
rect [0,0] [0.5,0.6] >> video;
```

`point` is just a pixel (and very small!). It has a fixed width and height, so the only arguments it needs are `[x,y]` coordinates
```
-- point [x,y,...]
point [0.5,0.5] >> video;
```

## Channels on the `video` output

We can replace any individual number with a set of multiple numbers. When we do this we get different results on different colour "channels" of the visual output - red, green, and blue:

```
vline [-0.25,0,0.25] 0.002 >> video;
```

This creates 3 different vlines
 - the 3 numbers in the brackets apply separately to the Red, Green and Blue channels of the `video` output
 - each different number positions each different coloured vline on the page

If you don't specify a position for at least three channels, remaining channels will be created with the last value in the list - this is because `video` is a 3 colour channel output, it will take a value per channel where possible.

```
vline [-0.25,0.25] 0.002 >> video;
```

***Note the line on the right is a light blue colour, the combination of both the Green and Blue (which was not specified) channels***

In this example, the first 3 positional values are assigned to individual colours in one RGB channel, but the last value is assigned to all the colours in the next RGB channel (so it displays as white)

```
vline [0.25, 0, -0.25, 0.5] 0.002 >> video;
```

Values can also be specified as fractions, using the Haskell Language syntax

```
vline [(-2)/3, 0, 2/3] 0.002 >> video;
```
---

# Exercises:

1. Try changing line width



2. Try specifying multiple different line widths



3. How wide is a line width of `1`?



## For Bonus Points

4. Using `vline`, can you make an entirely `blue` screen?



5. Can you split the screen into thirds, with each third showing a different RGB colour? (Hint: see the fraction example above)



---
