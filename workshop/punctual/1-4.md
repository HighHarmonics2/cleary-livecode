# Punctual Visual Workshop 1-4 - Jigging With the Numbers (~1h)

Ref: 
 - https://estuary.mcmaster.ca
 - https://github.com/dktr0/Punctual/blob/main/REFERENCE.md

## Setup Cells!

`!presetview twocolumns`

 - Left Cell: `Punctual`
 - Right Cell:  Your choice!

## Math Functions

Basic arithmetic can be applied inside patterns (addition, multiplication, division, subtraction, exponentiation)

```
-- basic signal
circle [0, (sin 0.2)] 0.25 >> video;

-- addition
circle [0, (sin 0.2)] (0.25 + sin 0.2) >> video;

-- multiplication
circle [0, (sin 0.2)] (0.25*2) >> video;

-- division
circle [0, (sin 0.2)/2] (0.25/2) >> video;

-- exponentiation
circle [0, (sin 0.2)] (0.25**(sin 0.2)) >> video;
```

You can compare the output between two graphs, and take the values only from the graph that matches the comparison function (eg `max`, `min`) 

```
circle [0, max (sin cps) (saw 1)] 0.25 >> video;
```

Punctual also has mathematical comparison functions (`>`, `<`, `==`, `>=`, `<=`, `!=` and more) that return a "boolean" value ie `1` (true) or `0` (false)

Let's continue with the `circle` examples from above, and try the "equality" function `==`:

```
circle 0 (sin 0.125) == circle 0.2 (tri 0.5) >> video;
```

What are you seeing here? Try identifying different sections of the image and determine what value you think the pixels in that section should have, based on the statement above

Apply the opposite "inequality" operator `!=` instead - think about how you expect the sections of the image to change, do they?

```
circle 0 (sin 0.125) != circle 0.2 (tri 0.5) >> video;
```

There are many more [Punctual Graph Functions](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md#punctual-graph-functions)


## Using Variables to Simplify Your Code

You may want to use some of the comparison functions with multiple complex statements, as you can imagine writing this on the one line can get unwieldy fast!

This example is not particularly complicated, but you can see it could get there quickly

```
max (circle (-0.2) (sin 0.125)) (circle 0.2 (tri 0.5)) >> video;
```

We can split the define the two statements generating circles separately, and store their output in a variable name (`a` and `b` in the example below). Then those variables can be referenced in a third statement for the `max` comparison function:

```
a << circle 0 (sin 0.125);
b << circle 0.2 (tri 0.5);
max a b >> video;
```
Much simpler!

## Connecting Transform Functions with `$`

Punctual includes 4 transform functions for shifting an image around on the canvas. 
 - `spin [amount] [...]`
 - `zoom [x,y] [...]`
 - `tile [x,y] [...]`
 - `move [x,y] [...]`

These functions can be used in combination with the haskell `$` operator

The least confusing technical description of `$` in haskell is

> "You usually see $ where standard function application wouldn’t have the necessary associativity and precedence for the context. And what *that* means is you usually see it used instead of parentheses to associate things that otherwise wouldn’t.
> [typeclasses.com](https://typeclasses.com/featured/dollar)

Let's do some testing by applying `spin` to a `hline`- starting with a static `hline`:

```
hline 0 0.2 >> video;
```

It is possible to apply spin using parentheses:

```
spin 1.25 (hline 0 0.2) >> video;
```

However, parentheses can be difficult to match, especially in a complicated punctual statement so let's **not** use them (for this example)!

```
spin 1.25 $ hline 0 0.2 >> video;
```

Much simpler! `spin` is applied to everything after the `$`! So easy, but...

What about if we want to apply multiple transform functions? Let's look at `spin` in combination with `tile`

First, let's get spin using a changing value - `tri 0.125`:

```
spin (tri 0.125) $ hline 0 0.2 >> video;
```

Next, let's add `tile` as a prefix to the entire statement:

```
tile 4 $ spin (tri 0.125) $ hline 0 0.2 >> video;
```

What's happening here? Is `spin` being applied before `tile` or afterwards?

If we consider what the output looks like, there are 16 (4x4) spinning lines, so a spinning line was created, and then tiled.

We can verify with parentheses:

```
tile 4 (spin (tri 0.125) $ hline 0 0.2) >> video;
```

Everything inside the parentheses is evaluated first, then `tile` is applied

Let's swap `tile` and `spin`:

```
spin (tri 0.125) $ tile 4 $ hline 0 0.2 >> video;
```

Describe what has happened here? If necessary, break the function into pieces, or use parentheses to verify 


## Image (texture) manipulation!

`tex` and `texhsv` are functions for accessing textures built from an image file (specified as a web url)

There is one major caveat in using these functions, the images must be hosted on a [CORS compliant](https://developer.mozilla.org/en-US/docs/Web/HTTP/CORS) web server - 

Fortunately, one of the biggest (and most interesting) repositories of permissively licensed images is CORS compliant:

    https://commons.wikimedia.org/wiki/Category:Images

Search for an image file, or pick one at random - then find the "Original File" link under the image, and right-click and "Copy link address"

Use your copied image address in the following examples (or use my example):

```
tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy >> video;
```

`texhsv` uses exactly the same syntax as `tex`, but outputs the texture content as a 3-channel `hsv` signal

```
texhsv "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy >> video;
```

What happens if you use the `hsv` output with `texhsv`?

```
texhsv "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy >> hsv;
```

What about using the `hsv` output with `tex`?

```
tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy >> hsv;
```

`tex` functions work well in conjuction with the transform functions covered earlier

```
zoom (prox (0.5 + sin 0.03)) $ tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy >> video;
```

## Transitions!

To reduce the harshness of what can sometimes be quite jarring changes, we can use a transition operator `<>`.

`<>` takes one argument, a floating point number in seconds to describe how long it should take to replace the original pattern with the new pattern, using a "fade" effect

Continuing on from the example above, what if we changed the image used by `tex`? Perfect time to apply a transition effect:

```
zoom (prox (0.5 + sin 0.03)) $ tex "https://upload.wikimedia.org/wikipedia/commons/4/43/Telephone_Box_with_a_View_%28geograph_2506046%29.jpg" fxy >> video <20>;
```

---

# Exercises:

1. The arithmetic operations can also be applied to patterns. Starting with the example code below, describe what happens when you replace the `<operator>` in the code

```
a << circle 0 0.25;
b << circle 0.15 0.35;
a <operator> b >> video;
```

 - addition `+`:
 - subtraction `-`:
 - multiplication `*`
 - division `/`
 - exponent `**`

---
